---
title: Stream Library
---

## Stream Library

The definitions provided by this library operate on streams and/or produce new
streams. This library also contains a number of streams representing special
sequences.

### Functions

#### <a name="downfrom"></a>downfrom (x: Numeric) :> [Numeric]

Produces a stream of numbers starting with the given number down through zero.

##### Examples

```
downfrom(5) = [ 5, 4, 3, 2, 1, 0]
downfrom(0) = [ 0 ]
downfrom(-1) = [ ]
downfrom(3.2) = [ 4, 3, 2, 1, 0 ]
```

##### Implementation

```
Y(
  (self, n) :> (
    (ceil(n) != n) : self(self, ceil(n))
    (n > 0) : [ n, self(self, n - 1) ]
    (n = 0) : [ 0 ]
    (     ) : [ ]
  )
)
```

#### foldl (x: T1, f: Lambda/2, s: [T2]) :> [T1]

##### Implementation

```
foldl := (
  folder := Y(
    (self, pad, function, stream) :> (
      (?stream) : (
        next := function(pad, stream');
        [ next, self(self, next, function, stream...) ]
      )
      ( ) : [ ]
    )
  );

  (initial, function, stream) :> (
    (?stream) : folder(initial, function, stream)
    (       ) : [ initial ]
  )
)
```

#### <a name="foldl1"></a>foldl1 (f: Lambda/2, s: [T]) :> [T]

##### Implementation

```
(function, stream) :> (
  (?stream) : foldl(stream', function, stream...)
  (       ) : [ ]
)
```

#### <a name="last"></a>last (s: [Any]) :> Any

Returns the last value in a finite stream. This function will walk an
infinite stream until it encounters some other limit such as running out of
processing time.

##### Examples

```
last( mean( 1..10 ) ) = 55
last( max( [ 1, 3, 5, 3, 1 ] ) ) = 5
```

##### Implementation

```
Y(
  (self, stream) :> (
    (?(stream...)) : self(self, stream...)
    (            ) : stream'
  )
)
```

#### <a name="make-evens"></a>make-evens () :> [ 2, 4, 6, ... ]

Creates a new stream consisting of the positive, non-zero even integers.

This is useful when you need a stream of even numbers and want to garbage
collect the stream after using it.

##### Examples

```
make-evens() = [ 2, 4, 6, ... ]
```

##### Implementation

```
() :> ({ # * 2 } @ 1..)
```

#### <a name="make-odds"></a>make-odds () :> [ 1, 3, 5, ... ]

Creates a new stream consisting of the positive, non-zero even integers.

This is useful when you need a stream of odd numbers and want to garbage
collect the stream after using it.

##### Examples

```
make-odds() = [ 1, 3, 5, ... ]
```

##### Implementation

```
() :> ({ # * 2 + 1} @ 0..)
```

#### <a name="range"></a>range (a: Numeric, b: Numeric) :> [Numeric]

Returns a stream of numbers starting at the first and increasing or decreasing
to the second.

##### Examples

```
range(1, 5) = [ 1, 2, 3, 4, 5 ]
range(5, 1) = [ 5, 4, 3, 2, 1 ]
```

##### Implementation

```
Y(
  (self, m, n) :> (
    (m > n) : [ m, self(self, m - 1, n) ]
    (m = n) : [ m ]
    (m < n) : [ m, self(self, m + 1, n) ]
    (     ) : [ ]
  )
)
```

#### <a name="upfrom"></a>upfrom (x: Numeric) :> [Numeric]

Produces an unbounded stream of increasing integers starting with the
given value.

##### Examples

```
upfrom(10) = [ 10, 11, 12, 13, ... ]
upfrom(-5) = [ -5, -4, -3, -2, -1, 0, 1, ...]
```

##### Implementation

```
Y(
  (self, n) :> [ n, self(self, n + 1) ]
)
```

### Streams

#### evens

##### Implementation

```
make-evens()
```

#### fibonacci-sequence


##### Implementation

```
[ 1,
  1,
  Y(
    (self, a, b) :>
      [ a + b, self(self, b, a + b) ]
  )(1, 1)
]
```

#### leonardo-sequence

##### Implementation

```
[ 1,
  1,
  Y(
    (self, a, b) :>
      [ a + b + 1, self(self, b, a + b + 1) ]
  )(1, 1)
]
```

#### odds

##### Implementation

```
make-odds()
```

#### primes

##### Implementation

```
[ 1,
  2,
  Y(
    (self, s) :>
      [ s', self(self, ~divisible-by?(_, s') % s...) ]
  )(make-odds()...)
]
```

#### twin-primes

##### Implementation

```
{ #[2] - #[1] = 2 } % (primes Z primes...)
```
